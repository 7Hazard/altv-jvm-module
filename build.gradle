import org.gradle.internal.os.OperatingSystem;

buildscript {
	repositories {
		mavenLocal()
		mavenCentral()
		jcenter()
	}
}

plugins {
	id "java"
	id "maven"
    id "de.undercouch.download" version "4.0.4"
}

repositories {
	// maven { url "https://jitpack.io" }
	mavenLocal()
	mavenCentral()
	jcenter()
}

group = 'com.gitlab.7Hazard'

sourceSets {
	main {
		java {
			srcDirs = ['src']
		}
	}
}

dependencies {
	// https://mvnrepository.com/artifact/com.github.jnr/jnr-ffi
	implementation 'com.github.jnr:jnr-ffi:2.1.9'
}

def platform = "unknown"
if(OperatingSystem.current().isWindows())
{
	platform = "win32"
}
else if(OperatingSystem.current().isLinux()) {
	platform = "linux"
}

def branch = "internal"
def buildnum = "0"
if(project.hasProperty('branch')) branch = project.getProperty("branch")
if(project.hasProperty('buildnum')) buildnum = project.getProperty("buildnum")
def version = "${branch}-${buildnum}"

tasks.withType(JavaCompile) {
  options.compilerArgs << "-Xlint:all" << "-Xlint:-rawtypes" << "-Werror"
}

jar {
	manifest {
		attributes(
			'Main-Class': 'hazard7.altv.jvm.Main',
		)
	}
	
	// javadoc.failOnError(false);
	// javadoc.source = sourceSets.main.allSource

	from configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
	from sourceSets.main.allSource
	// from javadoc.destinationDir
}

task getCAPI_win32(type: Download) {
	src 'http://github.com/7Hazard/altv-capi/releases/latest/download/altv-capi-server-win32.zip'
	dest buildDir
	overwrite false
}
task getCAPI_linux(type: Download) {
	src 'http://github.com/7Hazard/altv-capi/releases/latest/download/altv-capi-server-linux.zip'
	dest buildDir
	overwrite false
}
task dlCAPIJson(type: Download) {
	src 'http://github.com/7Hazard/altv-capi/releases/latest/download/altv-capi-server.json'
	dest buildDir
	overwrite false
}
task unzipCAPI_win32(type: Copy) {
  from zipTree("build/altv-capi-server-win32.zip")
  into buildDir
}
task unzipCAPI_linux(type: Copy) {
  from zipTree("build/altv-capi-server-linux.zip")
  into buildDir
}
getCAPI_win32.finalizedBy unzipCAPI_win32
getCAPI_linux.finalizedBy unzipCAPI_linux
getCAPI_win32.finalizedBy dlCAPIJson
getCAPI_linux.finalizedBy dlCAPIJson
if(!project.hasProperty('debug'))
	jar.dependsOn dlCAPIJson

if(System.env['JITPACK'] == null)
{
	task wrap(type: Exec) {
		standardOutput = out
		
		if(project.hasProperty('debug'))
		{
			commandLine "node", "wrap", "debug"
		}
		else
		{
			commandLine "node", "wrap"
			dependsOn "dlCAPIJson"
		}
	}
	compileJava.dependsOn wrap

	task buildModule(type: Exec) {
		standardOutput = out
		workingDir "module"

		if(project.hasProperty('debug'))
		{
			doFirst {
				assert OperatingSystem.current().isWindows()
				assert file("build/altv-capi-server-debug").exists()
			}
			
			commandLine "cmd", "/c", "build-debug.bat", version // windows
		}
		else {
			if(OperatingSystem.current().isWindows())
				commandLine "cmd", "/c", "build.bat", version // windows
			else
				commandLine "./build.sh", version // linux/mac
			
			dependsOn "getCAPI_$platform"
		}
	}

	task buildModuleWin32Cross(type: Exec) {
		standardOutput = out
		workingDir "module"
		
		commandLine "./build-win32.sh", version // linux/mac
	}
	buildModuleWin32Cross.dependsOn getCAPI_win32
	
	task copyToOutput(type: Copy) {
		// from jar.destinationDir
		from "build/libs/altv-jvm-module.jar"
		into "build/$platform/altv-jvm-module/"

		outputs.upToDateWhen { false }
	}
	jar.finalizedBy copyToOutput

	task copyToOutputWin32Cross(type: Copy) {
		from "build/libs/altv-jvm-module.jar"
		into "build/win32/altv-jvm-module/"

		outputs.upToDateWhen { false } 
	}
	buildModuleWin32Cross.dependsOn copyToOutputWin32Cross
}

///
/// Publishing
///

if(System.env['JITPACK'] != null)
{
	println "JITPACK BUILD"
}
